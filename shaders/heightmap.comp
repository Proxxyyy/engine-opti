#version 450

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
layout(r32f, binding = 0) uniform image2D uHeightmap;
layout(rgba16f, binding = 1) uniform image2D uNormalmap;

uniform float anim_time;
uniform vec2  uResolution = vec2(1024.0, 1024.0);


float fadef(float t) {
    return t * t * t * (t * (t * 6.0 - 15.0) + 10.0);
}

const int perm[256] = int[256](
    151,160,137,91,90,15,131,13,201,95,96,53,194,233,7,225,
    140,36,103,30,69,142,8,99,37,240,21,10,23,190,6,148,
    247,120,234,75,0,26,197,62,94,252,219,203,117,35,11,32,
    57,177,33,88,237,149,56,87,174,20,125,136,171,168, 68,175,
    74,165,71,134,139,48,27,166,77,146,158,231,83,111,229,122,
    60,211,133,230,220,105,92,41,55,46,245,40,244,102,143,54,
    65,25,63,161,1,216,80,73,209,76,132,187,208,89,18,169,
    200,196,135,130,116,188,159,86,164,100,109,198,173,186,3,64,
    52,217,226,250,124,123,5,202,38,147,118,126,255,82,85,212,
    207,206,59,227,47,16,58,17,182,189,28,42,223,183,170,213,
    119,248,152,2,44,154,163,70,221,153,101,155,167,43,172,9,
    129,22,39,253,19,98,108,110,79,113,224,232,178,185,112,104,
    218,246,97,228,251,34,242,193,238,210,144,12,191,179,162,241,
    81,51,145,235,249,14,239,107,49,192,214,31,181,199,106,157,
    184,84,204,176,115,121,50,45,127,4,150,254,138,236,205,93,
    222,114,67,29,24,72,243,141,128,195,78,66,215,61,156,180
);

const vec2 grad2[8] = vec2[8](
    vec2(1.0,0.0),
    vec2(-1.0,0.0),
    vec2(0.0,1.0),
    vec2(0.0,-1.0),
    vec2(0.70710678,0.70710678),
    vec2(-0.70710678,0.70710678),
    vec2(0.70710678,-0.70710678),
    vec2(-0.70710678,-0.70710678)
);

int permAt(int i){
    return perm[i & 255];
}

//Algorithme de Perlin
float perlin(vec2 p) {
    ivec2 ip = ivec2(floor(p));
    vec2 fp = fract(p);

    vec2 u = vec2(fadef(fp.x), fadef(fp.y));

    int aa = permAt(permAt(ip.x + 0) + ip.y + 0);
    int ab = permAt(permAt(ip.x + 0) + ip.y + 1);
    int ba = permAt(permAt(ip.x + 1) + ip.y + 0);
    int bb = permAt(permAt(ip.x + 1) + ip.y + 1);

    vec2 d00 = fp - vec2(0.0, 0.0);
    vec2 d10 = fp - vec2(1.0, 0.0);
    vec2 d01 = fp - vec2(0.0, 1.0);
    vec2 d11 = fp - vec2(1.0, 1.0);

    vec2 g00 = grad2[aa & 7];
    vec2 g10 = grad2[ba & 7];
    vec2 g01 = grad2[ab & 7];
    vec2 g11 = grad2[bb & 7];

    float v00 = dot(g00, d00);
    float v10 = dot(g10, d10);
    float v01 = dot(g01, d01);
    float v11 = dot(g11, d11);

    float ix0 = mix(v00, v10, u.x);
    float ix1 = mix(v01, v11, u.x);

    float value = mix(ix0, ix1, u.y);

    return clamp(value * 1.41421356, -1.0, 1.0);
}

float fbm(vec2 p, int octaves) {
    float value = 0.0;
    float amplitude = 0.5;
    float frequency = 1.0;
    for (int i = 0; i < octaves; ++i) {
        value += amplitude * perlin(p * frequency);
        frequency *= 2.0;
        amplitude *= 0.5;
    }
    return clamp(value, -1.0, 1.0);
}

float perlin_inv(vec2 p, int octaves) {
    float sum = 0.0;
    float amp = 0.5;
    float freq = 1.0;
    for (int i = 0; i < octaves; ++i) {
        float n = perlin(p * freq);
        n = 1.0 - abs(n);
        sum += n * amp;
        freq *= 2.0;
        amp *= 0.5;
    }
    return clamp(sum * 2.0 - 1.0, -1.0, 1.0);
}


void main() {
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);

    if (coord.x >= int(uResolution.x) || coord.y >= int(uResolution.y)) {
        return;
    }

    vec2 uv = (vec2(coord) + 0.5) / uResolution;

    float largeScale = 1.5;
    float midScale   = 6.0;
    float smallScale = 8.0;

    float largeScaleTime = 0.05;
    float midScaleTime   = 0.15;
    float smallScaleTime = 0.36;

    vec2 flow1 = vec2(0.12, -0.06);
    vec2 flow2 = vec2(-0.08, 0.14);
    vec2 flow3 = vec2(0.05, 0.22);

    float l1 = fbm(uv * largeScale + anim_time * largeScaleTime * flow1, 5) * 1.0;
    float l2 = fbm(uv * midScale   + anim_time * midScaleTime * flow2, 5) * 0.8;
    float l3 = perlin_inv(uv * smallScale * 0.6 + anim_time * midScaleTime * flow3, 4) * 0.6;

    float dir = dot(normalize(vec2(1.0, 0.2)), uv * 2.0 - 1.0);
    float streak = perlin(uv * 8.0 + anim_time * 0.1 + dir * 1.5) * 0.08;

    float total = l1 + l2 + l3 + streak;

    float amplitude = 0.8;
    float height = 0.5 + total * amplitude;

    height = clamp(height, 0.0, 2.0);

    imageStore(uHeightmap, coord, vec4(height, 0.0, 0.0, 1.0));


    // Normal pour chaque voisin
    vec2 texel = 1.0 / uResolution;
    vec2 uvL = clamp(uv + vec2(-texel.x, 0.0), vec2(0.0), vec2(1.0));
    vec2 uvR = clamp(uv + vec2(texel.x, 0.0), vec2(0.0), vec2(1.0));
    vec2 uvD = clamp(uv + vec2(0.0, -texel.y), vec2(0.0), vec2(1.0));
    vec2 uvU = clamp(uv + vec2(0.0, texel.y), vec2(0.0), vec2(1.0));

    //Calcul perlin pour les voisins
    float l1L = fbm(uvL * largeScale + anim_time * largeScaleTime * flow1, 5) * 1.0;
    float l2L = fbm(uvL * midScale   + anim_time * midScaleTime * flow2, 5) * 0.8;
    float l3L = perlin_inv(uvL * smallScale * 0.6 + anim_time * midScaleTime * flow3, 4) * 0.6;
    float streakL = perlin(uvL * 8.0 + anim_time * 0.1 + dot(normalize(vec2(1.0,0.2)), uvL * 2.0 - 1.0) * 1.5) * 0.08;
    float heightL = clamp(0.5 + (l1L + l2L + l3L + streakL) * amplitude, 0.0, 2.0);

    float l1R = fbm(uvR * largeScale + anim_time * largeScaleTime * flow1, 5) * 1.0;
    float l2R = fbm(uvR * midScale   + anim_time * midScaleTime * flow2, 5) * 0.8;
    float l3R = perlin_inv(uvR * smallScale * 0.6 + anim_time * midScaleTime * flow3, 4) * 0.6;
    float streakR = perlin(uvR * 8.0 + anim_time * 0.1 + dot(normalize(vec2(1.0,0.2)), uvR * 2.0 - 1.0) * 1.5) * 0.08;
    float heightR = clamp(0.5 + (l1R + l2R + l3R + streakR) * amplitude, 0.0, 2.0);

    float l1D = fbm(uvD * largeScale + anim_time * largeScaleTime * flow1, 5) * 1.0;
    float l2D = fbm(uvD * midScale   + anim_time * midScaleTime * flow2, 5) * 0.8;
    float l3D = perlin_inv(uvD * smallScale * 0.6 + anim_time * midScaleTime * flow3, 4) * 0.6;
    float streakD = perlin(uvD * 8.0 + anim_time * 0.1 + dot(normalize(vec2(1.0,0.2)), uvD * 2.0 - 1.0) * 1.5) * 0.08;
    float heightDown = clamp(0.5 + (l1D + l2D + l3D + streakD) * amplitude, 0.0, 2.0);

    float l1U = fbm(uvU * largeScale + anim_time * largeScaleTime * flow1, 5) * 1.0;
    float l2U = fbm(uvU * midScale   + anim_time * midScaleTime * flow2, 5) * 0.8;
    float l3U = perlin_inv(uvU * smallScale * 0.6 + anim_time * midScaleTime * flow3, 4) * 0.6;
    float streakU = perlin(uvU * 8.0 + anim_time * 0.1 + dot(normalize(vec2(1.0,0.2)), uvU * 2.0 - 1.0) * 1.5) * 0.08;
    float heightUp = clamp(0.5 + (l1U + l2U + l3U + streakU) * amplitude, 0.0, 2.0);

    float dhdx = (heightR - heightL) / (2.0 * texel.x);
    float dhdy = (heightUp - heightDown) / (2.0 * texel.y);

    vec3 n = normalize(vec3(-dhdx, 1.0, -dhdy));

    imageStore(uNormalmap, coord, vec4(n, 1.0));
}